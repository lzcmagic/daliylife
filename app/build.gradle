apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
android {
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file("lzcdev.properties")))
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.lzc.daliylife"
        minSdkVersion rootProject.ext.defaultConfig.minSdkVersion
        targetSdkVersion rootProject.ext.defaultConfig.targetSdkVersion
        versionCode rootProject.ext.defaultConfig.versionCode
        versionName rootProject.ext.defaultConfig.versionName
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        debug {
            keyPassword properties['keyPassword']
            storePassword properties['storePassword']
            storeFile rootProject.file(properties['storeFile'])
            keyAlias properties['keyAlias']
        }
        release {
            keyPassword properties['keyPassword']
            storePassword properties['storePassword']
            storeFile rootProject.file(properties['storeFile'])
            keyAlias properties['keyAlias']
        }
    }
    buildTypes {
        release {
            minifyEnabled true//混淆
            signingConfig signingConfigs.release
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "DaliyLife_V${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        if (project.hasProperty('OUT_PUT_DIR_PARA')) {
                            File output_dir1 = file("${OUT_PUT_DIR_PARA}");
                            output.outputFile = new File(output_dir1, fileName)
                            println "输出文件位置： " + output.outputFile
                        } else {
                            output.outputFile = new File(outputFile.parent, fileName)
                            println "输出文件位置： " + output.outputFile
                        }
                    }
                }
            }
        }
        debug {
            minifyEnabled false//混淆
            signingConfig signingConfigs.debug
            versionNameSuffix '-debug'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    productFlavors {
        lzc {
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    //support
    compile "com.android.support:appcompat-v7:${rootProject.ext.dependencies.support}"
    compile "com.android.support:cardview-v7:${rootProject.ext.dependencies.support}"
    compile "com.android.support:design:${rootProject.ext.dependencies.support}"
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.squareup.okio:okio:1.13.0'
    //gson
    compile 'com.google.code.gson:gson:2.8.0'
    //rxjava2
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //butterknife
    compile "com.jakewharton:butterknife:${rootProject.ext.dependencies.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.dependencies.butterknife}"
    //glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //picasso
    compile 'com.squareup.picasso:picasso:2.5.2'
    //photoview com.github.chrisbanes:PhotoView:latest.release.here
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    //alert
    compile 'com.tapadoo.android:alerter:1.0.6'
    //bugly
    compile 'com.tencent.bugly:crashreport:latest.release'
    compile 'com.tencent.bugly:nativecrashreport:latest.release'
    //浏览器支持
    compile 'com.android.support:customtabs:25.3.1'
    compile files('libs/commons-codec-1.10.jar')
}